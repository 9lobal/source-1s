// < vtrushnikov@1cbit.ru
// пример вызова 
// КонсольЗапросовОбщий.ВыгрузитьЗапросВФайлы(Запрос, "C:\Users\1cbit.trushnikov\Documents\КонсольЗапросов\Запросы\1");
Функция ВыгрузитьЗапросВФайлы(Запрос, Каталог) Экспорт
	Если ТипЗнч(Запрос) = Тип("Запрос") Тогда
		ВыгрузитьМенеджерВременныхТаблицВФайл(Запрос, Каталог + "\МенеджерВременныхТаблиц.xml");
		ВыгрузитьПараметрыЗапросаВФайл(Запрос, Каталог + "\ПараметрыЗапроса.xml");
		ВыгрузитьТекстЗапросаВФайл(Запрос, Каталог + "\Запрос.txt");
		Возврат Каталог;
	Иначе
		Возврат "Неверный параметр 1: ожидается Тип(""Запрос"")";
	КонецЕсли;
КонецФункции

Функция ВыгрузитьТекстЗапросаВФайл(Запрос, Путь) Экспорт
	Если Не ТипЗнч(Запрос) = Тип("Запрос") Тогда
		Возврат "Неверный параметр 1: ожидается Тип(""Запрос"")";
	КонецЕсли;
	ЗаписьТекста = Новый ЗаписьТекста;
	Попытка
		ЗаписьТекста.Открыть(Путь);
		ЗаписьТекста.Записать(СокрЛП(Запрос.Текст));
		ЗаписьТекста.Закрыть();
		Возврат Путь;
	Исключение
		Возврат ОписаниеОшибки()
	КонецПопытки;
КонецФункции

// пример вызова 
// КонсольЗапросовОбщий.ВыгрузитьМенеджерВременныхТаблицВФайл(МенеджерВременныхТаблиц, "C:\Users\1cbit.trushnikov\Documents\Конфигурация\ВыгрузкиМВТ\РассчитатьЗачетАвансовПолученных.xml");
Функция ВыгрузитьМенеджерВременныхТаблицВФайл(ЗапросИлиМВТ, Путь) Экспорт
	МВТ = Неопределено;
	Если ТипЗнч(ЗапросИлиМВТ) = Тип("МенеджерВременныхТаблиц") Тогда
		МВТ = ЗапросИлиМВТ;
	ИначеЕсли ТипЗнч(ЗапросИлиМВТ) = Тип("Запрос") Тогда
		МВТ = ЗапросИлиМВТ.МенеджерВременныхТаблиц;
	Иначе
		Возврат "Неверный параметр 1: ожидается Тип(""МенеджерВременныхТаблиц"") или Тип(""Запрос"")";
	КонецЕсли;
	Структура = Новый Структура;
	Для Каждого ВременнаяТаблица Из МВТ.Таблицы Цикл
		ИмяВременнойТаблицы = ВременнаяТаблица.ПолноеИмя;
		Структура.Вставить(ИмяВременнойТаблицы, ВременнаяТаблица.ПолучитьДанные().Выгрузить());
	КонецЦикла;
	ЗаписьТекста = Новый ЗаписьТекста;
	Попытка
		ЗаписьТекста.Открыть(Путь);
		ЗаписьТекста.Записать(Сериализовать(Структура));
		ЗаписьТекста.Закрыть();
		Возврат Путь;
	Исключение
		Возврат ОписаниеОшибки()
	КонецПопытки;
КонецФункции

// пример вызова 
// КонсольЗапросовОбщий.ВыгрузитьПараметрыЗапросаВФайл(Запрос, "C:\Users\1cbit.trushnikov\Documents\КонсольЗапросов\Запросы\1\Параметры.xml");
Функция ВыгрузитьПараметрыЗапросаВФайл(Запрос, Путь) Экспорт
	Если Не ТипЗнч(Запрос) = Тип("Запрос") Тогда
		Возврат "Неверный параметр 1: ожидается Тип(""Запрос"")";
	КонецЕсли;
	Структура = Новый Структура;
	Для Каждого ТекущийПараметр Из Запрос.Параметры Цикл
		Структура.Вставить(ТекущийПараметр.Ключ, ТекущийПараметр.Значение);
	КонецЦикла;
	ЗаписьТекста = Новый ЗаписьТекста;
	Попытка
		ЗаписьТекста.Открыть(Путь);
		ЗаписьТекста.Записать(Сериализовать(Структура));
		ЗаписьТекста.Закрыть();
		Возврат Путь;
	Исключение
		Возврат ОписаниеОшибки()
	КонецПопытки;
КонецФункции

Функция Десериализовать(XMLСтруктураСериализованногоОбъекта) Экспорт
	ЧтениеXMLДанных = Новый ЧтениеXML;
	ЧтениеXMLДанных.УстановитьСтроку(XMLСтруктураСериализованногоОбъекта);
	ТЗ = СериализаторXDTO.ПрочитатьXML(ЧтениеXMLДанных);
	ЧтениеXMLДанных.Закрыть();  
	Возврат ТЗ;
КонецФункции

Функция Сериализовать(ОбъектСериализации) Экспорт
	ДеревоВОбъектеXDTO = СериализаторXDTO.ЗаписатьXDTO(ОбъектСериализации);
	МойXML = Новый ЗаписьXML;
	МойXML.УстановитьСтроку();
	ФабрикаXDTO.ЗаписатьXML(МойXML, ДеревоВОбъектеXDTO);
	Возврат МойXML.Закрыть();
КонецФункции
// vtrushnikov@1cbit.ru >